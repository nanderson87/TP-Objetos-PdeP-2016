import escobas.*
import jugador.*
import posicionJugador.*
import equipo.*
import fixture.*
import suerte.*
import exceptions.*
import actividadesBuscador.*


test "La velocidad de una escoba Nimbus es (80 - su edad) * % de salud" {
	var nimbus2001_50hp = new Nimbus(2001,50)
	assert.that(nimbus2001_50hp.velocidad() == ((80-15)*0.5))
}

test "Harry Potter tiene 0,5 de manejo de escoba" {
	assert.equals(0.5, fixture.harryPotter().nivelManejoDeEscoba())
}

test "La escoba de Ron Weasley tiene una velocidad de 32 y por ende el de 16." {
	assert.equals( 16, fixture.ronWeasley().velocidad() )
}

test "Draco Malfoy tiene una velocidad de 35,1."{
	assert.equals(35.1, fixture.dracoMalfoy().velocidad())
}

test "Harry Potter tiene una velocidad 50." {
	assert.equals(50, fixture.harryPotter().velocidad())
}

test "Harry Potter, por ser buscador tiene una habilidad de 110." {
	assert.equals(110, fixture.harryPotter().habilidad())
}

test "Ginny Weasley, por ser cazadora tiene una habilidad de 282." {
	assert.equals(282, fixture.ginnyWeasley().habilidad())
}

test "Fred Weasley, por ser golpeador, tiene una habilidad de 240."{
	assert.equals(240, fixture.fredWeasley().habilidad())
}

test "Ron Weasley, por ser guardián, tiene una habilidad de 120,8." {
	assert.equals(120.8, fixture.ronWeasley().habilidad())
}

test "Fred Weasley le pasa el trapo a Miles Bletchley"{
	assert.that(fixture.fredWeasley().lePasaElTrapo(fixture.milesBletchley()))
} 

test "Fred Weasley NO le pasa el trapo a Warrington."{
	assert.that(not (fixture.fredWeasley().lePasaElTrapo(fixture.warrington())))
}


test "De Gryffindor Fred, Angelina y Katie son jugadores grosos"{
	assert.equals([fixture.angelinaJohnson(),fixture.fredWeasley(), fixture.katieBell()], fixture.gryffindor().jugadoresGrosos())
	}

test "De Slytherin  Warrington y  Montague son jugadores grosos" {
	assert.equals([fixture.grahamMontague(), fixture.warrington()],fixture.slytherin().jugadoresGrosos())	
}


test "Slytherin tiene jugador estrella contra Gryffindor"{
	assert.that(fixture.slytherin().jugadorEstrellaContra(fixture.gryffindor()))

}

test "Gryffindor NO tiene jugador estrella contra Slytherin"{
	assert.that(!(fixture.gryffindor().jugadorEstrellaContra(fixture.slytherin())))

}

test "Si Crabbe es golpeado por una bludger sus skills pasan a ser 78."{
	fixture.vincentCrabbe().golpearPorBludger(null)
	assert.equals(78,fixture.vincentCrabbe().skills())
}

test "Si Ron es golpeado por una bludger sus skills pasan a ser 28 y su Nimbus queda con 40% de salud."{
	fixture.ronWeasley().golpearPorBludger(null)
	assert.equals(28, fixture.ronWeasley().skills())
}

test "Si Ron es golpeado por una bludger su Nimbus queda con 40% de salud."{
	fixture.ronWeasley().golpearPorBludger(null)
	assert.equals(40, fixture.ronWeasley().escoba().salud())
}

test "Si Ginny que es cazadora obtiene la Quaffle, 
	debería ser ella quien tiene la Quaffle." {
	fixture.ginnyWeasley().obtenerLaQuaffle()
	
	assert.that(fixture.ginnyWeasley().tieneLaQuaffle())
}

test "Si la Quaffle está en manos de Ginny, 
	Katie no debería poder perder la Quaffle contra Slytherin." {
	fixture.ginnyWeasley().obtenerLaQuaffle()
	
	assert.throwsExceptionLike( new NoTengoQuaffle(),
          { => fixture.katieBell().perderQuaffleContra( fixture.slytherin() )} ) 
}

test "Si Harry estaba buscando la Snitch desde hace 3 turnos y lo golpea una Bludger de Slytherin, 
	debería perder los turnos de búsqueda acumulados."{
	fixture.harryPotter()
	fixture.harryPotter().actividad().aumentarTurno()
	fixture.harryPotter().actividad().aumentarTurno()
	fixture.harryPotter().actividad().aumentarTurno()
	fixture.harryPotter().golpearPorBludger(fixture.gregoryGoyle())
	assert.equals(0, fixture.harryPotter().actividad().turnos())
}

test "Si Goyle hace una jugada contra Gryffindor, 
	con Ginny luego de perder 2 puntos de skills pero teniendo la Quaffle y Harry buscando la Snitch, 
	elige como blanco a Ginny. Aunque tenga mala suerte puede golpear a Ginny, sus skills quedan en 75 y Ginny ya no tiene la Quaffle." {
	suerte.tipoDeSuerte(malaSuerte)
	fixture.ginnyWeasley().perderSkills(2)
	fixture.ginnyWeasley().obtenerLaQuaffle()
	fixture.gregoryGoyle().hacerJugada(fixture.gryffindor())
	
	assert.that(fixture.gregoryGoyle().skills()== 75 and !fixture.ginnyWeasley().tieneLaQuaffle())
}

test "Si Harry hace una jugada contra Slytherin con buena suerte, encuentra la Snitch.
	Si Harry está persiguiendo la Snitch estando a 4000 metros de distancia de atraparla y hace una jugada contra Slytherin, queda a 3900 metros de distancia." {
	suerte.tipoDeSuerte(buenaSuerte)
	fixture.harryPotter().hacerJugada(fixture.slytherin())
	suerte.tipoDeSuerte(suerteReal)
	fixture.harryPotter().hacerJugada(fixture.slytherin())

	assert.that(fixture.harryPotter().encontroSnitch() and fixture.harryPotter().actividad().metrosARecorrer() == 5000 - fixture.harryPotter().velocidad() * 2)
}

test "Si Goyle hace una jugada contra Gryffindor, 
	con Ginny teniendo la Quaffle y Harry buscando la Snitch, elige como blanco a Ginny (único blanco útil de Gryffindor). 
	Tiene mala suerte y no logra golpear a Ginny, sus skills quedan en 70 y Ginny sigue teniendo la Quaffle."{
	fixture.ginnyWeasley().obtenerLaQuaffle()
	suerte.tipoDeSuerte(malaSuerte)
	fixture.gregoryGoyle().hacerJugada(fixture.gryffindor())
	
	assert.that(fixture.gregoryGoyle().skills()== 70 and fixture.ginnyWeasley().tieneLaQuaffle())	
}

test "Un bloqueo de Gryffindor a Montague con buena suerte es exitoso, 
	con lo cual los skills de Montague quedan en 32 y los de Katie (la más veloz que puede bloquear) quedan en 48."{
	suerte.tipoDeSuerte(buenaSuerte)
	fixture.grahamMontague().obtenerLaQuaffle()
	fixture.grahamMontague().hacerJugada(fixture.gryffindor())
	assert.that(fixture.grahamMontague().skills() == 32 and fixture.katieBell().skills() == 48)	
}

test "Si Ron hace una jugada contra Slytherin, sus skills no varían y los puntos tampoco."{
	var skillsAntesRon = fixture.ronWeasley().skills()
	var puntajeAntesGryffindor = fixture.gryffindor().puntaje()
	fixture.ronWeasley().hacerJugada(fixture.slytherin())
	
	assert.that(fixture.ronWeasley().skills() == skillsAntesRon and fixture.gryffindor().puntaje() == puntajeAntesGryffindor) 
}
