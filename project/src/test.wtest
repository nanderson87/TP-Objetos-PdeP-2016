import escobas.*
import jugador.*
import posicionJugador.*
import equipo.*
import fixture.*
import exceptions.*
import actividades.*
import suerte.*


test "La velocidad de una escoba Nimbus es (80 - su edad) * % de salud" {
	var nimbus2001_50hp = new Nimbus(2001,50)
	assert.that(nimbus2001_50hp.velocidad() == ((80-15)*0.5))
}

test "Harry Potter tiene 0,5 de manejo de escoba" {
	assert.equals(0.5, fixture.harryPotter().nivelManejoDeEscoba())
}

test "La escoba de Ron Weasley tiene una velocidad de 32 y por ende el de 16." {
	assert.equals( 16, fixture.ronWeasley().velocidad() )
}

test "Draco Malfoy tiene una velocidad de 35,1."{
	assert.equals(35.1, fixture.dracoMalfoy().velocidad())
}

test "Harry Potter tiene una velocidad 50." {
	assert.equals(50, fixture.harryPotter().velocidad())
}

test "Harry Potter, por ser buscador tiene una habilidad de 110." {
	assert.equals(110, fixture.harryPotter().habilidad())
}

test "Ginny Weasley, por ser cazadora tiene una habilidad de 282." {
	assert.equals(282, fixture.ginnyWeasley().habilidad())
}

test "Fred Weasley, por ser golpeador, tiene una habilidad de 240."{
	assert.equals(240, fixture.fredWeasley().habilidad())
}

test "Ron Weasley, por ser guardian, tiene una habilidad de 120,8." {
	assert.equals(120.8, fixture.ronWeasley().habilidad())
}

test "Fred Weasley le pasa el trapo a Miles Bletchley"{
	assert.that(fixture.fredWeasley().lePasaElTrapo(fixture.milesBletchley()))
} 

test "Fred Weasley NO le pasa el trapo a Warrington."{
	assert.that(not (fixture.fredWeasley().lePasaElTrapo(fixture.warrington())))
}


test "De Gryffindor Fred, Angelina y Katie son jugadores grosos"{
	assert.equals([fixture.angelinaJohnson(),fixture.fredWeasley(), fixture.katieBell()], fixture.gryffindor().jugadoresGrosos())
	}

test "De Slytherin  Warrington y  Montague son jugadores grosos" {
	assert.equals([fixture.grahamMontague(), fixture.warrington()],fixture.slytherin().jugadoresGrosos())	
}


test "Slytherin tiene jugador estrella contra Gryffindor"{
	assert.that(fixture.slytherin().jugadorEstrellaContra(fixture.gryffindor()))

}

test "Gryffindor NO tiene jugador estrella contra Slytherin"{
	assert.that(!(fixture.gryffindor().jugadorEstrellaContra(fixture.slytherin())))

}

test "Si Crabbe es golpeado por una bludger sus skills pasan a ser 78."{
	fixture.vincentCrabbe().golpearPorBludger(null)
	assert.equals(78,fixture.vincentCrabbe().skills())
}

test "Si Ron es golpeado por una bludger sus skills pasan a ser 28 y su Nimbus queda con 40% de salud."{
	fixture.ronWeasley().golpearPorBludger(null)
	assert.equals(28, fixture.ronWeasley().skills())
}

test "Si Ron es golpeado por una bludger su Nimbus queda con 40% de salud."{
	fixture.ronWeasley().golpearPorBludger(null)
	assert.equals(40, fixture.ronWeasley().escoba().salud())
}

// Tests Entrega 2

test "1- Si Ginny tiene la Quaffle, Gryffindor debería tener la Quaffle y no Slytherin"{
	fixture.ginnyWeasley().obtenerQuaffle()
	assert.that(fixture.gryffindor().tieneQuaffle())
	assert.notThat(fixture.slytherin().tieneQuaffle())
}

test "2- Si Ginny obtiene la Quaffle, ella debería tener la Quaffle"{
	fixture.ginnyWeasley().obtenerQuaffle()
	assert.that(fixture.ginnyWeasley().tieneQuaffle())
}

test "3- Si Miles obtiene la Quaffle, debería pasársela a Graham"{
	fixture.milesBletchley().obtenerQuaffle()
	assert.that(fixture.grahamMontague().tieneQuaffle())
}

test "4- Si Slytherin obtiene la Quaffle, se la queda Warrington"{
	fixture.slytherin().obtenerQuaffle()
	assert.that(fixture.warrington().tieneQuaffle())
}

test "5- Si gryffindor obtiene la Quaffle, no debería poder obtenerla nuevamente"{
	fixture.gryffindor().obtenerQuaffle()
	assert.throwsExceptionLike(new YaTengoQuaffle(),{=>fixture.gryffindor().obtenerQuaffle()})
}

test "6- Si la Quaffle está en manos de Ginny y la pierde contra Slytherin, Warrington debería tener la Quaffle y no Ginny."{
	fixture.ginnyWeasley().obtenerQuaffle()
	fixture.ginnyWeasley().perderQuaffleContra(fixture.slytherin())
	assert.that(fixture.warrington().tieneQuaffle())
	assert.notThat(fixture.ginnyWeasley().tieneQuaffle())
}

test "7- Si la Quaffle está en manos de Ginny, Katie no debería poder perder la Quaffle contra Slytherin."{
	fixture.ginnyWeasley().obtenerQuaffle()
	assert.throwsExceptionLike(new NoTengoQuaffle(),{=>fixture.katieBell().perderQuaffleContra(fixture.slytherin())})
}

test "8- Si la Quaffle está en manos de Ginny y es golpeada por una Bludger de Slytherin, Slytherin debería tener la Quaffle."{
	fixture.ginnyWeasley().obtenerQuaffle()
	fixture.ginnyWeasley().golpearPorBludger(fixture.dracoMalfoy())
	assert.that(fixture.slytherin().tieneQuaffle())
}

test "9- Si Harry que está empezando la búsqueda de la Snitch hace una jugada contra Slytherin con mala suerte, al terminar su jugada no encontró la Snitch."{
	fixture.harryPotter().actividad(new Busqueda())
	suerte.tipoDeSuerte(malaSuerte)
	fixture.harryPotter().hacerJugada(null)
	assert.notThat(fixture.harryPotter().encontroSnitch())
}

test "10.1- Si Harry hace una jugada contra Slytherin con buena suerte, encuentra la Snitch."{
	suerte.tipoDeSuerte(buenaSuerte)
	fixture.harryPotter().hacerJugada(fixture.slytherin())
	assert.that(fixture.harryPotter().encontroSnitch())
}

test "10.2- Si Harry está persiguiendo la Snitch estando a 4000 metros de distancia de atraparla y hace una jugada contra Slytherin, queda a 3900 metros de distancia."{
	fixture.harryPotter().actividad(new Persecucion())
	fixture.harryPotter().actividad().distancia(4000)
	fixture.harryPotter().hacerJugada(fixture.slytherin())
	assert.equals(3900,fixture.harryPotter().actividad().distancia())
}

test "11- Si Gryffindor tiene 30 puntos y Harry está persiguiendo la Snitch estando a 75 metros de distancia de atraparla y hace una jugada contra Slytherin, Gryffindor queda con 180 puntos y los skills de Harry quedan en 60."{
	fixture.gryffindor().ganarPuntos(30)
	fixture.harryPotter().actividad(new Persecucion())
	fixture.harryPotter().actividad().distancia(75)
	fixture.harryPotter().hacerJugada(fixture.slytherin())
	assert.equals(180,fixture.gryffindor().puntos())
	assert.equals(60,fixture.harryPotter().skills())
}

test "12- Si Harry estaba buscando la Snitch desde hace 3 turnos y lo golpea una Bludger de Slytherin, debería perder los turnos de búsqueda acumulados."{
	fixture.harryPotter().actividad().aumentarTurno()
	fixture.harryPotter().actividad().aumentarTurno()
	fixture.harryPotter().actividad().aumentarTurno()
	fixture.harryPotter().golpearPorBludger(fixture.vincentCrabbe())
	assert.equals(0,fixture.harryPotter().actividad().turnos())
}

test "13- Si Harry estaba persiguiendo la Snitch estando a 75 metros de distancia de atraparla y lo golpea una Bludger de Slytherin, debería haber perdido de vista la Snitch, y al encontrarla nuevamente estará a 5000 metros de distancia."{
	fixture.harryPotter().actividad(new Persecucion())
	fixture.harryPotter().actividad().distancia(75)
	fixture.harryPotter().golpearPorBludger(fixture.vincentCrabbe())
	suerte.tipoDeSuerte(buenaSuerte)
	assert.notThat(fixture.harryPotter().encontroSnitch())
	fixture.harryPotter().hacerJugada(fixture.slytherin())
	assert.equals(5000,fixture.harryPotter().actividad().distancia())
}

test "14- Si Harry (luego de ganar 100 puntos de skills) estaba persiguiendo la Snitch estando a 75 metros de distancia de atraparla y lo golpea una Bludger de Slytherin, no debería haber perdido de vista la Snitch porque sólo quedó aturdido. Luego de hacer una jugada debería seguir estando a 75 metros, y luego de una segunda jugada debería tener 158 de skills por atrapar la Snitch."{
	fixture.harryPotter().ganarSkills(100)
	fixture.harryPotter().actividad(new Persecucion())
	fixture.harryPotter().actividad().distancia(75)
	fixture.harryPotter().golpearPorBludger(fixture.vincentCrabbe())
	assert.that(fixture.harryPotter().encontroSnitch())
	fixture.harryPotter().hacerJugada(fixture.slytherin())
	assert.equals(75,fixture.harryPotter().actividad().distancia())
	fixture.harryPotter().hacerJugada(fixture.slytherin())
	assert.equals(158,fixture.harryPotter().skills())
}

test "15- Si Fred hace una jugada contra Slytherin, con Ginny teniendo la Quaffle y Malfoy a 100 metros de conseguir la Snitch, elige como blanco (de entre Malfoy, Montague y Bletchley) a Montague. Su puntería no es suficiente alta, pero con buena suerte lo golpea y sus skills quedan en 85, mientras que los de Montague quedan en 33."{
	fixture.ginnyWeasley().obtenerQuaffle()
	fixture.dracoMalfoy().actividad(new Persecucion())
	fixture.dracoMalfoy().actividad().distancia(100)
	assert.equals(fixture.fredWeasley().mejorBlanco(fixture.slytherin()),fixture.grahamMontague())
	suerte.tipoDeSuerte(buenaSuerte)
	fixture.fredWeasley().hacerJugada(fixture.slytherin())
	assert.equals(85,fixture.fredWeasley().skills())
	assert.equals(33,fixture.grahamMontague().skills())
}

test "16- Si Goyle hace una jugada contra Gryffindor, con Ginny teniendo la Quaffle y Harry buscando la Snitch, elige como blanco a Ginny (único blanco útil de Gryffindor). Tiene mala suerte y no logra golpear a Ginny, sus skills quedan en 70 y Ginny sigue teniendo la Quaffle."{
	fixture.ginnyWeasley().obtenerQuaffle()
	suerte.tipoDeSuerte(malaSuerte)
	assert.equals(fixture.ginnyWeasley(),fixture.gregoryGoyle().mejorBlanco(fixture.gryffindor()))
	fixture.gregoryGoyle().hacerJugada(fixture.gryffindor())
	assert.equals(70,fixture.gregoryGoyle().skills())
	assert.that(fixture.ginnyWeasley().tieneQuaffle())
}

test "17- Si Goyle hace una jugada contra Gryffindor, con Ginny luego de perder 2 puntos de skills pero teniendo la Quaffle y Harry buscando la Snitch, elige como blanco a Ginny. Aunque tenga mala suerte puede golpear a Ginny, sus skills quedan en 75 y Ginny ya no tiene la Quaffle."{
	fixture.ginnyWeasley().obtenerQuaffle()
	fixture.ginnyWeasley().perderSkills(2)
	suerte.tipoDeSuerte(malaSuerte)
	assert.equals(fixture.ginnyWeasley(),fixture.gregoryGoyle().mejorBlanco(fixture.gryffindor()))
	fixture.gregoryGoyle().hacerJugada(fixture.gryffindor())
	assert.equals(75,fixture.gregoryGoyle().skills())
	assert.notThat(fixture.ginnyWeasley().tieneQuaffle())
}

test "18- Un bloqueo de Slytherin a Katie, incluso con mala suerte, es exitoso porque Montague le pasa el trapo, con lo cual Katie queda con 42 puntos de skills y Montague con 38."{
	suerte.tipoDeSuerte(malaSuerte)
	fixture.slytherin().bloquear(fixture.katieBell())
	assert.equals(42,fixture.katieBell().skills())
	assert.equals(38,fixture.grahamMontague().skills())
}

test "19. Un bloqueo de Gryffindor a Montague con mala suerte no es exitoso. No deberían modificarse los skills de Montague."{
	suerte.tipoDeSuerte(malaSuerte)
	var skills = fixture.grahamMontague().skills()
	assert.throwsExceptionLike(new NoPudeBloquear(),{=>fixture.gryffindor().bloquear(fixture.grahamMontague())})
	assert.equals(skills,fixture.grahamMontague().skills())	
}

test "20. Un bloqueo de Gryffindor a Montague con buena suerte es exitoso, con lo cual los skills de Montague quedan en 32 y los de Katie (la más veloz que puede bloquear) quedan en 48."{
	suerte.tipoDeSuerte(buenaSuerte)
	fixture.gryffindor().bloquear(fixture.grahamMontague())
	assert.equals(32,fixture.grahamMontague().skills())
	assert.equals(48,fixture.katieBell().skills())
}

test "21- Si Ron gana 200 puntos de skills y cambia su escoba por una Nimbus 2016 en perfecto estado, un bloqueo de Gryffindor a Montague con mala suerte será exitoso porque Ron le pasa el trapo. Los skills de Montague quedan en 32 y los de Ron, que es guardián, quedan en 240."{
	fixture.ronWeasley().escoba(new Nimbus(2016,100))
	fixture.ronWeasley().ganarSkills(200)
	suerte.tipoDeSuerte(malaSuerte)
	fixture.gryffindor().bloquear(fixture.grahamMontague())
	assert.equals(32,fixture.grahamMontague().skills())
	assert.equals(240,fixture.ronWeasley().skills())
}

test "22- Si Warrington tiene la Quaffle y Montague hace una jugada contra Gryffindor, sus skills no varían, los puntos tampoco y la Quaffle la sigue teniendo Warrington."{
	fixture.warrington().obtenerQuaffle()
	var skills = fixture.grahamMontague().skills()
	var puntos = fixture.slytherin().puntos()
	fixture.grahamMontague().hacerJugada(fixture.gryffindor())
	assert.equals(skills,fixture.grahamMontague().skills())
	assert.equals(puntos,fixture.slytherin().puntos())
}

test "23- Si Montague tiene la Quaffle, hace una jugada contra Gryffindor y no lo bloquean porque tienen mala suerte, sus skills quedan en 40, Slytherin gana 10 puntos y Gryffindor se queda con la Quaffle."{
	fixture.grahamMontague().obtenerQuaffle()
	suerte.tipoDeSuerte(malaSuerte)
	fixture.grahamMontague().hacerJugada(fixture.gryffindor())
	assert.equals(40,fixture.grahamMontague().skills())
	assert.equals(10,fixture.slytherin().puntos())
	assert.that(fixture.gryffindor().tieneQuaffle())
}

test "24- Si Katie hace una jugada contra Slytherin, como es cazadora Gryffindor no gana puntos y Slytherin se queda con la Quaffle."{
	fixture.katieBell().obtenerQuaffle()
	fixture.katieBell().hacerJugada(fixture.slytherin())
	assert.equals(0,fixture.gryffindor().puntos())
	assert.that(fixture.slytherin().tieneQuaffle())
}

test "25- Si Ron hace una jugada contra Slytherin, sus skills no varían y los puntos tampoco."{
	var skills = fixture.ronWeasley().skills()
	fixture.ronWeasley().hacerJugada(fixture.slytherin())
	assert.equals(skills,fixture.ronWeasley().skills())
	assert.equals(0,fixture.gryffindor().puntos())
}